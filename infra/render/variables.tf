variable "environment" {
  description = "Logical environment name used for tagging/naming conventions."
  type        = string
  default     = "production"

  validation {
    condition     = contains(["production", "staging", "development"], var.environment)
    error_message = "The environment variable must be one of: production, staging, development."
  }
}

variable "region" {
  description = "Render region for all production resources."
  type        = string
  default     = "singapore"

  validation {
    condition     = contains(["singapore", "oregon", "frankfurt", "ohio"], var.region)
    error_message = "Render region must be one of: singapore, oregon, frankfurt, ohio."
  }
}

variable "project_environment_id" {
  description = "Optional existing Render project environment ID, such as the Production environment inside the UITFood project."
  type        = string
  default     = null
}

variable "web_service_name" {
  description = "Render service name for the web frontend."
  type        = string
  default     = "UITFood Web"
}

variable "api_service_name" {
  description = "Render service name for the API."
  type        = string
  default     = "UITFood API"
}

variable "gateway_service_name" {
  description = "Render service name for the edge gateway."
  type        = string
  default     = "UITFood Gateway"
}

variable "media_service_name" {
  description = "Private-network DNS/service name for the Media service."
  type        = string
  default     = "uitfood-media"
}

variable "postgres_name" {
  description = "Render Postgres instance name."
  type        = string
  default     = "UITFood Postgres"
}

variable "media_postgres_name" {
  description = "Render Postgres instance name dedicated to Media."
  type        = string
  default     = "UITFood Media Postgres"
}

variable "service_plan" {
  description = "Render web service plan."
  type        = string
  default     = "free"

  validation {
    condition     = contains(["free", "starter", "standard", "pro", "plus"], var.service_plan)
    error_message = "The service_plan must be a valid Render plan: free, starter, standard, pro, or plus."
  }
}

variable "media_service_plan" {
  description = "Render private services require a paid compute plan."
  type        = string
  default     = "starter"

  validation {
    condition     = contains(["starter", "standard", "pro", "pro_plus", "pro_max", "pro_ultra"], var.media_service_plan)
    error_message = "media_service_plan must be a private-service capable Render plan."
  }
}

variable "postgres_plan" {
  description = "Render Postgres plan."
  type        = string
  default     = "free"

  validation {
    condition     = contains(["free", "starter", "standard", "pro", "plus"], var.postgres_plan)
    error_message = "The postgres_plan must be a valid Render plan: free, starter, standard, pro, or plus."
  }
}

variable "media_postgres_plan" {
  description = "Render Postgres plan for the Media-owned database."
  type        = string
  default     = "free"

  validation {
    condition     = contains(["free", "starter", "standard", "pro", "plus"], var.media_postgres_plan)
    error_message = "media_postgres_plan must be a valid Render Postgres plan."
  }
}

variable "postgres_version" {
  description = "Postgres major version."
  type        = string
  default     = "18"
}

variable "postgres_database_name" {
  description = "Application database name."
  type        = string
  default     = "uitfood_db"
}

variable "postgres_database_user" {
  description = "Application database user."
  type        = string
  default     = "nestjs"
}

variable "media_postgres_database_name" {
  description = "Immutable logical database name owned by Media."
  type        = string
  default     = "uitfood_media"
}

variable "media_postgres_database_user" {
  description = "Immutable database user owned by Media."
  type        = string
  default     = "uitfood_media"
}

variable "postgres_ip_allow_list" {
  description = "CIDR ranges allowed to connect to Postgres externally. Use an empty list to allow private-network connections only."
  type = list(object({
    cidr_block  = string
    description = string
  }))
  default = []
}

variable "api_image_url" {
  description = "Container image repository for the API, without a tag."
  type        = string
  default     = "ghcr.io/ndtruongdanh/uitfood-api"
}

variable "web_image_url" {
  description = "Container image repository for the web frontend, without a tag."
  type        = string
  default     = "ghcr.io/ndtruongdanh/uitfood-web"
}

variable "gateway_image_url" {
  description = "Container image repository for the gateway, without a tag."
  type        = string
  default     = "ghcr.io/ndtruongdanh/uitfood-gateway"
}

variable "media_image_url" {
  description = "Container image repository for Media, without a tag."
  type        = string
  default     = "ghcr.io/ndtruongdanh/uitfood-media"
}

variable "api_image_tag" {
  description = "Container image tag for the API service."
  type        = string
}

variable "gateway_image_tag" {
  description = "Container image tag for the gateway service."
  type        = string
}

variable "media_image_tag" {
  description = "Container image tag for the Media service."
  type        = string
}

variable "web_image_tag" {
  description = "Container image tag for the web service."
  type        = string
}

variable "api_custom_domains" {
  description = "Custom domains attached to the API service."
  type = set(object({
    name = string
  }))
  default = null
}

variable "web_custom_domains" {
  description = "Custom domains attached to the web service."
  type = set(object({
    name = string
  }))
  default = null
}

variable "gateway_custom_domains" {
  description = "Custom domains attached to the gateway service. Once cut over, the public domain points here instead of the API."
  type = set(object({
    name = string
  }))
  default = null
}

variable "api_health_check_path" {
  description = "Optional API health check path."
  type        = string
  default     = "/api/ready"
}

variable "gateway_health_check_path" {
  description = "Gateway health check path."
  type        = string
  default     = "/ready"
}

variable "web_health_check_path" {
  description = "Optional web health check path."
  type        = string
  default     = "/healthz"
}

variable "api_env_vars" {
  description = "API service environment variables managed by Terraform. Values are sent to Render on plan/apply and stored in Terraform state."
  type        = map(string)
  default     = {}
  sensitive   = true
}

variable "web_env_vars" {
  description = "Web service environment variables managed by Terraform. Values are sent to Render on plan/apply and stored in Terraform state."
  type        = map(string)
  default     = {}
  sensitive   = true
}

variable "api_env_group_id" {
  description = "Optional existing Render environment group ID that contains API runtime secrets."
  type        = string
  default     = ""
}

variable "gateway_env_vars" {
  description = "Gateway service environment variables managed by Terraform (e.g. GATEWAY_PROXY_TIMEOUT_MS). Values are stored in Terraform state."
  type        = map(string)
  default     = {}
  sensitive   = true
}

variable "media_env_vars" {
  description = "Media-only runtime variables, including Cloudinary credentials."
  type        = map(string)
  default     = {}
  sensitive   = true
}

variable "gateway_monolith_upstream_url" {
  description = "Upstream URL the gateway proxies to. When empty, defaults to the managed API service URL."
  type        = string
  default     = ""
}

variable "media_tcp_host" {
  description = "Optional private DNS override; defaults to the Media service slug."
  type        = string
  default     = ""
}

variable "media_tcp_port" {
  description = "Primary private Nest TCP listener port."
  type        = number
  default     = 10000
}

variable "media_management_port" {
  description = "Secondary private HTTP management port."
  type        = number
  default     = 10001
}

variable "media_rpc_timeout_ms" {
  description = "Gateway/API deadline for Media TCP requests."
  type        = number
  default     = 2000
}

variable "media_rpc_max_attempts" {
  description = "Bounded Catalog retries for idempotent image creation."
  type        = number
  default     = 2
}

variable "gateway_auth_timeout_ms" {
  description = "Deadline for the transitional monolith session check."
  type        = number
  default     = 3000
}

variable "gateway_cors_origins" {
  description = "Comma-separated browser origins allowed on Gateway-owned routes."
  type        = string
  default     = "http://localhost:5173,http://localhost:5174"
}

variable "media_routes_enabled" {
  description = "Cutover switch: Gateway owns /api/images and /api/cloudinary."
  type        = bool
  default     = false
}

variable "legacy_media_routes_enabled" {
  description = "Rollback switch for legacy API Media routes. Disable at cutover."
  type        = bool
  default     = true
}
